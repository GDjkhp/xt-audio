project (xt-audio)
cmake_minimum_required (VERSION 3.10)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Core library (C API).
set (CORE_DIR "../src/core/xt")
file (GLOB_RECURSE CORE_SRC "${CORE_DIR}/*.*")
add_library (xt-core SHARED ${CORE_SRC})
target_compile_options (xt-core PRIVATE -DXT_EXPORT=1)
target_include_directories (xt-core PRIVATE ${CORE_DIR})
if (WIN32)
  source_group(TREE "../../${CORE_DIR}" FILES ${CORE_SRC})
  set_target_properties(xt-core PROPERTIES RUNTIME_OUTPUT_DIRECTORY "../../../dist/core/xt")
endif ()
if (UNIX)
  set_target_properties(xt-core PROPERTIES LIBRARY_OUTPUT_DIRECTORY "../../../../dist/core/xt/${CMAKE_BUILD_TYPE}")
endif ()

# C++ library (C++ API).
set (CPP_DIR "../src/cpp/xt")
file (GLOB_RECURSE CPP_SRC "${CPP_DIR}/*.*")
add_library(xt-cpp INTERFACE)
target_sources(xt-cpp INTERFACE ${CPP_SRC})
target_include_directories (xt-cpp INTERFACE ${CORE_DIR})
add_library(xt-cpp_ ${CPP_SRC})
target_link_libraries (xt-cpp_ xt-core)
target_include_directories (xt-cpp_ PRIVATE ${CPP_DIR})
target_include_directories (xt-cpp_ PRIVATE ${CORE_DIR})
set_target_properties(xt-cpp_ PROPERTIES LINKER_LANGUAGE CXX)
if (WIN32)
  source_group(TREE "../../${CPP_DIR}" FILES ${CPP_SRC})
endif ()

# C++ sample program.
set (SAMPLE_DIR "../src/cpp/sample")
file (GLOB SAMPLE_SRC "${SAMPLE_DIR}/*.*")
add_executable (xt-sample ${SAMPLE_SRC})
target_link_libraries (xt-sample xt-core)
target_include_directories (xt-sample PRIVATE ${CPP_DIR})
target_include_directories (xt-sample PRIVATE ${CORE_DIR})
if (WIN32)
  source_group(TREE "../../${SAMPLE_DIR}" FILES ${SAMPLE_SRC})
  set_target_properties(xt-sample PROPERTIES RUNTIME_OUTPUT_DIRECTORY "../../../dist/cpp/sample")
endif ()
if (UNIX)
  set_target_properties(xt-sample PROPERTIES RUNTIME_OUTPUT_DIRECTORY "../../../../dist/cpp/sample/${CMAKE_BUILD_TYPE}")
endif ()

# Runtime dependencies.
if (UNIX)
  target_link_libraries (xt-core pthread)
endif ()
if (UNIX AND XT_ENABLE_JACK)
  target_link_libraries (xt-core jack)
endif ()
if (UNIX AND XT_ENABLE_ALSA)
  target_link_libraries (xt-core asound)
endif ()
if (WIN32 AND XT_ENABLE_WASAPI)
  target_link_libraries (xt-core avrt)
endif ()
if (UNIX AND XT_ENABLE_PULSE)
  target_link_libraries (xt-core pulse-simple pulse)
endif ()
if (WIN32 AND XT_ENABLE_DSOUND)
  target_link_libraries (xt-core dsound dxguid winmm)
endif ()

# Source code dependencies.
if (WIN32 AND XT_ENABLE_ASIO)
  file (GLOB_RECURSE ASMJIT_SRC "${XT_ASMJIT_DIR}/*.*")
  add_library (asmjit STATIC ${ASMJIT_SRC})
  target_compile_options (asmjit PUBLIC -DASMJIT_STATIC=1)
  target_include_directories (xt-core PRIVATE ${XT_ASMJIT_DIR})
  target_link_libraries (xt-core asmjit)
endif ()

# Source code dependencies.
if (WIN32 AND XT_ENABLE_ASIO)
  file (GLOB ASIOSDK_SRC "${XT_ASIOSDK_DIR}/host/asiodrivers.cpp"
                         "${XT_ASIOSDK_DIR}/host/pc/asiolist.cpp")
  add_library (asiosdk STATIC ${ASIOSDK_SRC})
  target_include_directories (asiosdk PRIVATE ${XT_ASIOSDK_DIR}/common)
  target_include_directories (asiosdk PRIVATE ${XT_ASIOSDK_DIR}/host/pc)
  target_include_directories (xt-core PRIVATE ${XT_ASIOSDK_DIR})
  target_link_libraries (xt-core asiosdk)
endif ()

# Backend selection.
target_compile_options (xt-core PRIVATE -DXT_ENABLE_ASIO=${XT_ENABLE_ASIO})
target_compile_options (xt-core PRIVATE -DXT_ENABLE_ALSA=${XT_ENABLE_ALSA})
target_compile_options (xt-core PRIVATE -DXT_ENABLE_JACK=${XT_ENABLE_JACK})
target_compile_options (xt-core PRIVATE -DXT_ENABLE_PULSE=${XT_ENABLE_PULSE})
target_compile_options (xt-core PRIVATE -DXT_ENABLE_WASAPI=${XT_ENABLE_WASAPI})
target_compile_options (xt-core PRIVATE -DXT_ENABLE_DSOUND=${XT_ENABLE_DSOUND})